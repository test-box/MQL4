//--------------------------------------------------------------------
// closeorder.mq4
// Предназначен для использования в качестве примера в учебнике MQL4.
//--------------------------------------------------------------- 1 --
int start()                                     // Спец.функция start
  {
   string Symb=Symbol();                      // Финанс. инструмент
   double Dist=1000000.0;                     // Предустановка
   int Real_Order=-1;                         // Пока рыночных нет
   double Win_Price=WindowPriceOnDropped();     // Здесь брошен скрипт
//--------------------------------------------------------------- 2 --
   for(int i=1; i<=OrdersTotal(); i++)         // Цикл перебора ордер
     {
      if (OrderSelect(i-1,SELECT_BY_POS)==true) // Если есть следующий
        {                                      // Анализ ордеров:
         //------------------------------------------------------ 3 --
         if (OrderSymbol()!= Symb) continue;    // Не наш фин.инструм.
         int Tip=OrderType();                 // Тип ордера
         if (Tip>1) continue;                   // Отложенный ордер  
         //------------------------------------------------------ 4 --
         double Price=OrderOpenPrice();       // Цена ордера
         if (NormalizeDouble(MathAbs(Price-Win_Price),Digits)< //Выбор
            NormalizeDouble(Dist,Digits))       // самого близкого орд       
           {
            Dist=MathAbs(Price-Win_Price);      // Новое значение
            Real_Order=Tip;                   // Есть рыночный ордер
            int Ticket=OrderTicket();         // Номер ордера
            double Lot=OrderLots();           // Количество лотов
           }
         //------------------------------------------------------ 5 --
        }                                      //Конец анализа ордера
     }                                         //Конец перебора орд.
//--------------------------------------------------------------- 6 --
   while(true)                                 // Цикл закрытия орд.
     {
      if (Real_Order==-1)                     // Если рыночных нет
        {
         Alert("По ",Symb," рыночных ордеров нет");
         break;                                 // Выход из цикла закр        
        }
      //--------------------------------------------------------- 7 --
      switch(Real_Order)                       // По типу ордера
        {
         case 0: double Price_Cls=Bid;        // Ордер Buy
            string Text="Buy ";          // Текст для Buy
            break;                         // Из switch
         case 1: Price_Cls=Ask;               // Ордер Sell
            Text="Sell ";                // Текст для Sell
        }
      Alert("Попытка закрыть ",Text," ",Ticket,". Ожидание ответа..");
      bool Ans=OrderClose(Ticket,Lot,Price_Cls,2);// Закрытие ордера
      //--------------------------------------------------------- 8 --
      if (Ans==true)                            // Получилось :)
        {
         Alert ("Закрыт ордер ",Text," ",Ticket);
         break;                                 // Выход из цикла закр
        }
      //--------------------------------------------------------- 9 --
      int Error=GetLastError();               // Не получилось :(
      switch(Error)                            // Преодолимые ошибки
        {
         case 135:Alert("Цена изменилась. Пробуем ещё раз..");
            RefreshRates();               // Обновим данные
            continue;                     // На след. итерацию
         case 136:Alert("Нет цен. Ждём новый тик..");
            while(RefreshRates()==false)  // До нового тика
               Sleep(1);                  // Задержка в цикле
            continue;                     // На след. итерацию
         case 146:Alert("Подсистема торговли занята. Пробуем ещё..");
            Sleep(500);                   // Простое решение
            RefreshRates();               // Обновим данные
            continue;                     // На след. итерацию
        }
      switch(Error)                       // Критические ошибки
        {
         case 2 : Alert("Общая ошибка.");
            break;                        // Выход из switch
         case 5 : Alert("Старая версия клиентского терминала.");
            break;                        // Выход из switch
         case 64: Alert("Счет заблокирован.");
            break;                        // Выход из switch
         case 133:Alert("Торговля запрещена");
            break;                        // Выход из switch
         default: Alert("Возникла ошибка ",Error);//Другие варианты   
        }
      break;                                    // Выход из цикла закр
     }
//-------------------------------------------------------------- 10 --
   Alert ("Скрипт закончил работу -----------------------------");
   return;                                      // Выход из start()
  }
//-------------------------------------------------------------- 11 --